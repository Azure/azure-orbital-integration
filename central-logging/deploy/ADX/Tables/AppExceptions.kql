// Copyright (c) 2022 Microsoft Corporation. All rights reserved.
// Software is licensed under the MIT License. See LICENSE in the project
// root for license information.

// Configures staging table, expanded table, expand function and update policies.
// This will allow external log data to land in staging and get expanded to the final table
.create-merge table Staging_AppExceptions (Records: dynamic)
.alter-merge table Staging_AppExceptions policy update @'[{ "IsTransactional": true}]'
.alter-merge table Staging_AppExceptions policy retention softdelete = 0s recoverability = disabled
.create-or-alter table Staging_AppExceptions ingestion json mapping "AppExceptions_Staging_Mapping" '[{"column":"Records","path":"$.records","datatype":"","transform":null}]'

.create-merge table AppExceptions (
    PrimaryCorrelationId: string
    ,SecondaryCorrelationId: string
    ,AppRoleInstance: string
    ,AppVersion: string
    ,Assembly: string
    ,ClientCountryOrRegion: string
    ,ClientIP: string
    ,ClientType: string
    ,Details: dynamic
    ,ExceptionType: string
    ,IKey: string
    ,InnermostAssembly: string
    ,InnermostMessage: string
    ,InnermostMethod: string
    ,InnermostType: string
    ,ItemCount: int
    ,Method: string
    ,OperationId: string
    ,OuterMessage: string
    ,OuterType: string
    ,ParentId: string
    ,ProblemId: string
    ,Properties: dynamic
    ,ResourceGUID: string
    ,SDKVersion: string
    ,SeverityLevel: int
    ,SourceSystem: string
    ,TenantId: string
    ,TimeGenerated: datetime
    ,Type: string
    ,_BilledSize: real
    ,_Internal_WorkspaceResourceId: string
    ,_ResourceId: string)

.create-or-alter function 
with (docstring = 'Expands Staging_AppExceptions to AppExceptions', folder='Expands')
Expand_AppExceptions() {
    Staging_AppExceptions
    | mv-expand Records
    | extend events = Records
    | extend properties = todynamic(extract_json("$", tostring(events.Properties)))
    | extend message = todynamic(extract_json("$", tostring(properties.FormattedMessage)))
    | project
        ["PrimaryCorrelationId"] = tostring(properties.["PrimaryCorrelationId"])
        ,["SecondaryCorrelationId"] = tostring(message.["SecondaryCorrelationId"])
        ,["AppRoleInstance"] = tostring(events.["AppRoleInstance"])
        ,["AppVersion"] = tostring(events.["AppVersion"])
        ,["Assembly"] = tostring(events.["Assembly"])
        ,["ClientCountryOrRegion"] = tostring(events.["ClientCountryOrRegion"])
        ,["ClientIP"] = tostring(events.["ClientIP"])
        ,["ClientType"] = tostring(events.["ClientType"])
        ,["Details"] = todynamic(events.["Details"])
        ,["ExceptionType"] = tostring(events.["ExceptionType"])
        ,["IKey"] = tostring(events.["IKey"])
        ,["InnermostAssembly"] = tostring(events.["InnermostAssembly"])
        ,["InnermostMessage"] = tostring(events.["InnermostMessage"])
        ,["InnermostMethod"] = tostring(events.["InnermostMethod"])
        ,["InnermostType"] = tostring(events.["InnermostType"])
        ,["ItemCount"] = toint(events.["ItemCount"])
        ,["Method"] = tostring(events.["Method"])
        ,["OperationId"] = tostring(events.["OperationId"])
        ,["OuterMessage"] = tostring(events.["OuterMessage"])
        ,["OuterType"] = tostring(events.["OuterType"])
        ,["ParentId"] = tostring(events.["ParentId"])
        ,["ProblemId"] = tostring(events.["ProblemId"])
        ,["Properties"] = todynamic(events.["Properties"])
        ,["ResourceGUID"] = tostring(events.["ResourceGUID"])
        ,["SDKVersion"] = tostring(events.["SDKVersion"])
        ,["SeverityLevel"] = toint(events.["SeverityLevel"])
        ,["SourceSystem"] = tostring(events.["SourceSystem"])
        ,["TenantId"] = tostring(events.["TenantId"])
        ,["TimeGenerated"] = todatetime(events.["TimeGenerated"])
        ,["Type"] = tostring(events.["Type"])
        ,["_BilledSize"] = toreal(events.["_BilledSize"])
        ,["_Internal_WorkspaceResourceId"] = tostring(events.["_Internal_WorkspaceResourceId"])
        ,["_ResourceId"] = tostring(events.["_ResourceId"])
}

.alter table AppExceptions policy update @'[{"Source": "Staging_AppExceptions", "Query": "Expand_AppExceptions()", "IsEnabled": "True", "IsTransactional": true}]'
